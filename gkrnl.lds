ENTRY("start")
OUTPUT_ARCH("i386")
OUTPUT_FORMAT("elf32-i386")
SECTIONS {
    . = 0x00100000;

    _kernel_start = .;

    .multiboot.data : {
        *(.multiboot.data)
    }

    .multiboot.text : {
        *(.multiboot.text)
    }

    . += 0xC0000000;

    .text ALIGN(4K) : AT (ADDR (.text) - 0xC0000000) {
        *(.text)
    }

    .data ALIGN(4K) : AT (ADDR (.data) - 0xC0000000) {
        *(.data)
    }

    .rodata ALIGN(4K) : AT (ADDR (.rodata) - 0xC0000000) {
        *(.rodata)
    }

    .bss ALIGN(4K) : AT (ADDR (.bss) - 0xC0000000) {
        *(COMMON)
        *(.bss)
        *(.boot_stack)
    }

    _kernel_end = .;
}